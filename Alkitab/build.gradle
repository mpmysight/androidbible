buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


apply plugin: 'me.tatarka.retrolambda'

android {
    signingConfigs {
        release {
            keyAlias System.getenv("SIGN_ALIAS")
            keyPassword System.getenv("SIGN_PASSWORD")
            storeFile file(System.getenv("SIGN_KEYSTORE") != null ? System.getenv("SIGN_KEYSTORE") : "/dev/null")
            storePassword System.getenv("SIGN_PASSWORD")
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId 'yuku.alkitab.debug'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 14000350
        versionName '4.5.0'
        multiDexEnabled true
        // Keep this synced with integrate_translations.sh!
        resConfigs 'af', 'bg', 'cs', 'da', 'de', 'es', 'fr', 'in', 'ja', 'ko', 'lv', 'ms', 'nl', 'pl', 'pt', 'ro', 'ru', 'th', 'uk', 'vi', 'zh-rCN', 'zh-rTW'
        buildConfigField 'String', 'SERVER_HOST', "\"$SERVER_HOST\""
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dexOptions {
        javaMaxHeapSize "2g"
        // Skip pre-dexing when running on Travis CI
        preDexLibraries = !travisBuild
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        // Use this for development
        plain {
        }

        // The following flavors are for release
        yuku_alkitab {
            applicationId 'yuku.alkitab'
        }
        yuku_quick_bible {
            applicationId 'yuku.alkitab.kjv'
        }
        sabda_alkitab {
            applicationId 'org.sabda.alkitab'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':AlkitabFeedback')
    compile project(':AlkitabIntegration')
    compile project(':AlkitabIo')
    compile project(':AlkitabModel')
    compile project(':AlkitabYes2')
    compile project(':BiblePlus')
    compile project(':DragSortListView')
    compile project(':KpriModel')
    compile project(':BintexReader')
    compile project(':BintexWriter')
    compile project(':Afw')
    compile project(':FlowLayout')
    compile project(':AmbilWarna')
    compile project(':ATree')
    compile project(':ImportedDesktopVerseUtil')
    compile "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    compile "com.afollestad.material-dialogs:commons:$materialDialogsVersion"
    testCompile 'junit:junit:4.12'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    debugCompile "com.facebook.stetho:stetho:$stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.squareup.picasso:picasso:$picassoVersion"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2"
    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "com.google.android.gms:play-services-analytics:$playServicesVersion"
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:percent:$supportLibVersion"
    compile "com.takisoft.fix:preference-v7:24.2.0.0"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile 'com.mindorks.android:prdownloader:0.2.0'
}
